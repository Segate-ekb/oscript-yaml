#Использовать 1connector
#Использовать fs
#Использовать logos

Перем Логгер;


Функция ПолучитьТегиСДанными(АдресРепозитория)
	
	Логгер.Информация("Получение тегов с данными из репозитория: %1", АдресРепозитория);

	Адрес = СтрШаблон("https://api.github.com/repos/%1/tags", АдресРепозитория);
	ТегиРепозитория = КоннекторHTTP.Get(Адрес).Json();

	ТегиСДанными = Новый ТаблицаЗначений();
	ТегиСДанными.Колонки.Добавить("Имя");
	ТегиСДанными.Колонки.Добавить("Адрес");

	Для каждого ТегИсточника Из ТегиРепозитория Цикл
		Если Не СтрНачинаетсяС(ТегИсточника["name"], "data-") Тогда
			Продолжить;
		КонецЕсли;

		Тег = ТегиСДанными.Добавить();
		Тег.Имя = ТегИсточника["name"];
		Тег.Адрес = ТегИсточника["zipball_url"];
	КонецЦикла;

	ТегиСДанными.Сортировать("Имя");

	Логгер.Отладка("Получены теги с данными: %1",
		СтрСоединить(ТегиСДанными.ВыгрузитьКолонку("Имя"), ", "));

	Возврат ТегиСДанными;

КонецФункции

Функция ПутьККаталогуТестов()

	Возврат ОбъединитьПути("tests", "fixtures", "yaml-test-suite");

КонецФункции

Функция ПутьКФайлуСостояния()

	Возврат ОбъединитьПути(ПутьККаталогуТестов(), "state.txt");

КонецФункции

Функция ПолучитьПоследнееСостояниеТестов()

	ПутьКФайлу = ПутьКФайлуСостояния();
	Если Не ФС.ФайлСуществует(ПутьКФайлу) Тогда
		Возврат "";
	КонецЕсли;

	ЧтениеТекста = Новый ЧтениеТекста(ПутьКФайлу, "UTF-8");
	ПоследнееСостояние = СокрЛП(ЧтениеТекста.Прочитать());
	ЧтениеТекста.Закрыть();

	Логгер.Информация("Получено последнее состояние тестов: %1", ПоследнееСостояние);

	Возврат ПоследнееСостояние;

КонецФункции

Процедура ЗаписатьСостояниеТестов(Состояние)

	ПутьКФайлу = ПутьКФайлуСостояния();

	ЗаписьТекста = Новый ЗаписьТекста(ПутьКФайлу, "UTF-8");
	ЗаписьТекста.Записать(Состояние);
	ЗаписьТекста.Закрыть();

	Логгер.Информация("Обновлено состояние тестов: %1", Состояние);

КонецПроцедуры

Функция ПутьКФайлуНереализованных()

	Возврат ОбъединитьПути(ПутьККаталогуТестов(), "not-implemented.txt");

КонецФункции

Функция ПолучитьТестыНереализованнойФункциональности()

	ПутьКФайлу = ПутьКФайлуНереализованных();
	Если Не ФС.ФайлСуществует(ПутьКФайлу) Тогда
		Возврат "";
	КонецЕсли;

	ЧтениеТекста = Новый ЧтениеТекста(ПутьКФайлу, "UTF-8");
	СписокНереализованных = ЧтениеТекста.Прочитать();
	ЧтениеТекста.Закрыть();

	Логгер.Информация("Получены тесты нереализованной функциональности: %1",
		СтрЧислоСтрок(СокрЛП(СписокНереализованных)));

	Возврат СписокНереализованных;

КонецФункции

Процедура ЗаписатьТестыНереализованнойФункциональности(СписокНереализованных)

	ПутьКФайлу = ПутьКФайлуНереализованных();

	ЗаписьТекста = Новый ЗаписьТекста(ПутьКФайлу, "UTF-8");
	ЗаписьТекста.Записать(СписокНереализованных);
	ЗаписьТекста.Закрыть();

	Логгер.Информация("Обновлены тесты нереализованной функциональности: %1", 
		СтрЧислоСтрок(СокрЛП(СписокНереализованных)));

КонецПроцедуры

Процедура ПолучитьИРаспаковатьАрхив(ЦелевойКаталог, АдресАрхива)
	
	Логгер.Информация("Получение архива с тестами: %1", АдресАрхива);

	ДанныеАрхива = КоннекторHTTP.Get(АдресАрхива).ДвоичныеДанные();
	ПотокДанныхАрхива = ДанныеАрхива.ОткрытьПотокДляЧтения();

	Логгер.Информация("Распаковка архива в каталог: %1", ЦелевойКаталог);

	ЧтениеZipФайла = Новый ЧтениеZipФайла(ПотокДанныхАрхива);
	ЧтениеZipФайла.ИзвлечьВсе(ЦелевойКаталог);
	ЧтениеZipФайла.Закрыть();

	ПотокДанныхАрхива.Закрыть();

	Логгер.Информация("Удаление вложенного корневого каталога");

	ВложенныйКаталог = НайтиФайлы(ЦелевойКаталог, "", Ложь)[0].ПолноеИмя;
	ФС.КопироватьСодержимоеКаталога(ВложенныйКаталог, ЦелевойКаталог);
	ФС.УдалитьФайлы(ВложенныйКаталог, , Истина);
	УдалитьФайлы(ВложенныйКаталог);

КонецПроцедуры

Процедура УдалитьНеактуальныеФайлы(Каталог)

	Логгер.Информация("Удаление неактуальных файлов в каталоге: %1", Каталог);

	Маски = Новый Массив;
	Маски.Добавить("out.yaml");
	Маски.Добавить("test.event");
	Маски.Добавить("emit.yaml");

	Для каждого Маска Из Маски Цикл
		ФС.УдалитьФайлы(Каталог, Маска, Истина);
	КонецЦикла;

КонецПроцедуры

Процедура СоздатьТестовыеСценарииПоТегам()
	ИмяШаблонаСьюта = ОбъединитьПути(ТекущийСценарий().Каталог, "suite-template");
	Если Не ФС.ФайлСуществует(ИмяШаблонаСьюта) Тогда
		Логгер.Ошибка("Шаблон тестового набора <%1> не найден! Пропускаем создание сценариев", ИмяШаблонаСьюта);
		Возврат;
	КонецЕсли;
	ИмяШаблонаТеста = ОбъединитьПути(ТекущийСценарий().Каталог, "test-template");
	Если Не ФС.ФайлСуществует(ИмяШаблонаТеста) Тогда
		Логгер.Ошибка("Шаблон тестового сценария <%1> не найден! Пропускаем создание сценариев", ИмяШаблонаТеста);
		Возврат;
	КонецЕсли;

	ТекстТестовогоНабора = ПолучитьТекстФайла(ИмяШаблонаСьюта);
	ТекстШаблонаТеста = ПолучитьТекстФайла(ИмяШаблонаТеста);
	ПутьКТестовымДанным = ПутьККаталогуТестов();
	ФайлыТестов = НайтиФайлы(ПутьКТестовымДанным, "===", Истина);
	Для каждого Каталог Из ФайлыТестов Цикл
		ОтносительныйПуть = ФС.ОтносительныйПуть(ПутьКТестовымДанным, Каталог.Путь);
		НормализованныйПуть = СтрЗаменить(ОтносительныйПуть, "/", "_");
		НормализованныйПуть = СтрЗаменить(НормализованныйПуть, "\", "_");
		Описание = ОтносительныйПуть+": "+СтрЗаменить(СокрЛП(ПолучитьТекстФайла(Каталог.ПолноеИмя)),"""","""""");
		СтрокаТегов = ПолучитьОписаниеТегов(ОтносительныйПуть);
		ТекстТестовогоНабора = ТекстТестовогоНабора + Символы.ПС + СтрШаблон(
			ТекстШаблонаТеста, Описание, СтрокаТегов, НормализованныйПуть, ОтносительныйПуть);
	КонецЦикла;
	ПутьКТестовомуСценарию = ОбъединитьПути("tests", "yaml_test_suite.os");
	ЗаписатьТестовыйНабор(ТекстТестовогоНабора, ПутьКТестовомуСценарию);
КонецПроцедуры

Функция ПолучитьОписаниеТегов(Каталог)
	ОписаниеТегов = "";
	ПутьКТегам = ОбъединитьПути(ПутьККаталогуТестов(), "tags");
	КаталогиТегов = НайтиФайлы(ПутьКТегам, "*", Ложь);

	Для каждого КаталогТега Из КаталогиТегов Цикл
		Если Не КаталогТега.ЭтоКаталог() Тогда
			Продолжить;
		КонецЕсли;
		ПутьКОписаниюТеста = ОбъединитьПути(КаталогТега.ПолноеИмя, Каталог);
		Файл = Новый Файл(ПутьКОписаниюТеста);
		Если Файл.Существует() Тогда
			ИмяТега = СтрЗаменить(КаталогТега.Имя, ".", "_");
			ИмяТега = СтрЗаменить(ИмяТега, "-", "_");
			ТекстТега = "&Тег("""+ИмяТега+""")";
			ОписаниеТегов = ОписаниеТегов + ?(ОписаниеТегов = "", "", Символы.ПС) + ТекстТега;
			Логгер.Отладка("Добавлен тег: %1", ТекстТега);
		КонецЕсли;

	КонецЦикла;
	Возврат ОписаниеТегов;
	
КонецФункции

Функция ПолучитьТекстФайла(ИмяФайла)

	ЧтениеТекста = Новый ЧтениеТекста(ИмяФайла, "UTF-8");
	ТекстФайла = ЧтениеТекста.Прочитать();
	ЧтениеТекста.Закрыть();

	Логгер.Отладка("Получен текст: %1", ИмяФайла);

	Возврат ТекстФайла;

КонецФункции

Процедура ЗаписатьТестовыйНабор(ТекстФайла, ПутьКФайлу)

	ЗаписьТекста = Новый ЗаписьТекста(ПутьКФайлу, "UTF-8");
	ЗаписьТекста.Записать(ТекстФайла);
	ЗаписьТекста.Закрыть();

	Логгер.Информация("Создан тестовый сценарий <%1>", ПутьКФайлу);

КонецПроцедуры

Процедура ВыполнитьСкрипт()
	
	Логгер = Логирование.ПолучитьЛог("oscript.app.yaml-test-suite");
	Логгер.УстановитьУровень(УровниЛога.Информация);

	АдресРепозитория = "yaml/yaml-test-suite";

	Теги = ПолучитьТегиСДанными(АдресРепозитория);
	АктуальныйТег = Теги[Теги.Количество() - 1];

	ИмяПоследнегоТега = ПолучитьПоследнееСостояниеТестов();
	Если ИмяПоследнегоТега = АктуальныйТег.Имя Тогда
		Логгер.Информация("Тесты уже обновлены до актуального состояния: %1", АктуальныйТег.Имя);
		ЗавершитьРаботу(0);
	КонецЕсли;

	Нереализованные = ПолучитьТестыНереализованнойФункциональности();

	КаталогДляРаспаковки = ПутьККаталогуТестов();
	ФС.ОбеспечитьПустойКаталог(КаталогДляРаспаковки);
	ПолучитьИРаспаковатьАрхив(КаталогДляРаспаковки, АктуальныйТег.Адрес);
	УдалитьНеактуальныеФайлы(КаталогДляРаспаковки);

	ЗаписатьТестыНереализованнойФункциональности(Нереализованные);
	ЗаписатьСостояниеТестов(АктуальныйТег.Имя);

	СоздатьТестовыеСценарииПоТегам();
КонецПроцедуры


ВыполнитьСкрипт();
