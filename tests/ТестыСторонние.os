#Использовать asserts
#Использовать ".."
#Использовать "."


&ТестовыйНабор
Процедура ПриСозданииОбъекта() Экспорт

	// В качестве источника тестов используется:
	// https://github.com/yaml/yaml-test-suite.git

КонецПроцедуры

&ПараметризованныйТест
&ИсточникЗначение("3ALJ")
&ИсточникЗначение("4RWC")
Процедура ТестКорректногоYAML(Каталог, Описание) Экспорт
	
	ПутьККаталогу = ТестовыеУтилиты.ПолучитьПутьКТестовымДанным("yaml-test-suit/" + Каталог + "/");

	СодержимоеYAML = ТестовыеУтилиты.ПрочитатьТекстФайла(ПутьККаталогу + "in.yaml");
	ЧтениеYaml = ТестовыеУтилиты.СоздатьЭкземплярПарсера();
	РезультатYAML = ЧтениеYaml.ПрочитатьYaml(СодержимоеYAML);	
	РезультатСтрокой = ДанныеВСтрокуJSON(РезультатYAML);

	Образец = ПрочитатьJSONизФайла(ПутьККаталогу + "in.json");
	ОбразецСтрокой = ДанныеВСтрокуJSON(Образец);

	Ожидаем.Что(РезультатСтрокой).Равно(ОбразецСтрокой);

КонецПроцедуры

&ПараметризованныйТест
&ИсточникЗначение("2CMS")
Процедура ТестНекорректногоYAML(Каталог, Описание) Экспорт
	
	ПутьККаталогу = ТестовыеУтилиты.ПолучитьПутьКТестовымДанным("yaml-test-suit/" + Каталог + "/");

	СодержимоеYAML = ТестовыеУтилиты.ПрочитатьТекстФайла(ПутьККаталогу + "in.yaml");
	ЧтениеYaml = ТестовыеУтилиты.СоздатьЭкземплярПарсера();

	МассивПараметров = Новый Массив;
	МассивПараметров.Добавить(СодержимоеYAML);

	Ожидаем.Что(ЧтениеYaml)
		.Метод("ПрочитатьYaml", МассивПараметров)
		.ВыбрасываетИсключение();

КонецПроцедуры

Функция ПрочитатьJSONизФайла(ПутьКФайлу)
	
	ЧтениеJSON = Новый ЧтениеJSON();
	ЧтениеJSON.ОткрытьФайл(ПутьКФайлу);
	Результат = ПрочитатьJSON(ЧтениеJSON, Истина);
	ЧтениеJSON.Закрыть();
	
	Возврат Результат;

КонецФункции

Функция ДанныеВСтрокуJSON(Данные)
	
	ПараметрыЗаписиJSON = Новый ПараметрыЗаписиJSON(
		ПереносСтрокJSON.Нет,
	);
	
	ЗаписьJSON = Новый ЗаписьJSON();
	ЗаписьJSON.УстановитьСтроку(ПараметрыЗаписиJSON);
	ЗаписатьJSON(ЗаписьJSON, Данные);
	Результат = ЗаписьJSON.Закрыть();
	
	Возврат Результат;

КонецФункции

Функция ЕстьФайлОшибки(ПутьККаталогу)
	
	ПутьКФайлуОшибки = ПутьККаталогу + "error";
	ФайлОшибки = Новый Файл(ПутьКФайлуОшибки);
	
	Возврат ФайлОшибки.Существует();

КонецФункции
