#Использовать asserts
#Использовать collectionos
#Использовать fs
#Использовать ".."
#Использовать "."


&ТестовыйНабор
Процедура ПриСозданииОбъекта()

	// В качестве источника тестов используется:
	// https://github.com/yaml/yaml-test-suite.git

КонецПроцедуры

&ПараметризованныйТест
&ИсточникВыражение("() -> КоллекцияКаталоговТестовыхДанных()")
Процедура ТестКорректногоYAML(Каталог, Описание) Экспорт
	
	ПутьККаталогу = ОбъединитьПути(ПутьКТестовымДанным(), Каталог);

	ПутьYAML = ОбъединитьПути(ПутьККаталогу, "in.yaml");
	СодержимоеYAML = ТестовыеУтилиты.ПрочитатьТекстФайла(ПутьYAML);
	ЧтениеYaml = ТестовыеУтилиты.СоздатьЭкземплярПарсера();

	РезультатYAML = ЧтениеYaml.ПрочитатьYaml(СодержимоеYAML);
	РезультатСтрокой = ДанныеВСтрокуJSON(РезультатYAML);

	ПутьJSON = ОбъединитьПути(ПутьККаталогу, "in.json");
	Образец = ПрочитатьJSONизФайла(ПутьJSON);
	ОбразецСтрокой = ДанныеВСтрокуJSON(Образец);

	Ожидаем.Что(РезультатСтрокой).Равно(ОбразецСтрокой);

КонецПроцедуры

&ПараметризованныйТест
&ИсточникВыражение("() -> КоллекцияКаталоговТестовыхДанных(Истина)")
Процедура ТестНекорректногоYAML(Каталог, Описание) Экспорт
	
	ПутьККаталогу = ОбъединитьПути(ПутьКТестовымДанным(), Каталог);

	ПутьYAML = ОбъединитьПути(ПутьККаталогу, "in.yaml");
	СодержимоеYAML = ТестовыеУтилиты.ПрочитатьТекстФайла(ПутьYAML);
	ЧтениеYaml = ТестовыеУтилиты.СоздатьЭкземплярПарсера();

	МассивПараметров = Новый Массив;
	МассивПараметров.Добавить(СодержимоеYAML);

	Ожидаем.Что(ЧтениеYaml)
		.Метод("ПрочитатьYaml", МассивПараметров)
		.ВыбрасываетИсключение();

КонецПроцедуры

Функция КоллекцияКаталоговТестовыхДанных(ИскатьОшибки = Ложь) Экспорт

	НайденныеФайлы = НайтиФайлы(ПутьКТестовымДанным(), "===", Истина);

	КоллекцияАргументов = Новый Массив;

	Для Каждого Файл Из НайденныеФайлы Цикл
		Если ЕстьФайлОшибки(Файл.Путь) <> ИскатьОшибки Тогда
			Продолжить;
		КонецЕсли;

		ОписаниеТеста = СокрЛП(ТестовыеУтилиты.ПрочитатьТекстФайла(Файл.ПолноеИмя));
		КаталогТеста = ФС.ОтносительныйПуть(ПутьКТестовымДанным(), Файл.Путь);
		АргументыТеста = Списки.ИзЭлементов(КаталогТеста, ОписаниеТеста);
		КоллекцияАргументов.Добавить(АргументыТеста);
	КонецЦикла;

	Возврат ПроцессорыКоллекций.ИзКоллекции(КоллекцияАргументов);

КонецФункции

Функция ПрочитатьJSONизФайла(ПутьКФайлу)
	
	ЧтениеJSON = Новый ЧтениеJSON();
	ЧтениеJSON.ОткрытьФайл(ПутьКФайлу);
	Результат = ПрочитатьJSON(ЧтениеJSON, Истина);
	ЧтениеJSON.Закрыть();
	
	Возврат Результат;

КонецФункции

Функция ДанныеВСтрокуJSON(Данные)
	
	ПараметрыЗаписиJSON = Новый ПараметрыЗаписиJSON(
		ПереносСтрокJSON.Нет,
	);
	
	ЗаписьJSON = Новый ЗаписьJSON();
	ЗаписьJSON.УстановитьСтроку(ПараметрыЗаписиJSON);
	ЗаписатьJSON(ЗаписьJSON, Данные);
	Результат = ЗаписьJSON.Закрыть();
	
	Возврат Результат;

КонецФункции

Функция ЕстьФайлОшибки(ПутьККаталогу)
	
	ПутьКФайлуОшибки = ОбъединитьПути(ПутьККаталогу, "error");
	Возврат ФС.ФайлСуществует(ПутьКФайлуОшибки);

КонецФункции

Функция ПутьКТестовымДанным()

	Возврат ТестовыеУтилиты.ПолучитьПутьКТестовымДанным("yaml-test-suite");

КонецФункции
